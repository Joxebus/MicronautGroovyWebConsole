plugins {
    id("groovy")
    id("com.github.johnrengelman.shadow") version "7.1.1"
    id("io.micronaut.application") version "3.2.0"
}

ext {
    groovyVersion = "3.0.9"
    dropboxVersion = "4.0.1"
}

version = "1.0"
group = "com.github.joxebus"

repositories {
    mavenCentral()
}

micronaut {
    runtime("netty")
    testRuntime("spock2")
    processing {
        incremental(true)
        annotations("com.github.joxebus.*")
    }
}

dependencies {
    implementation("javax.inject:javax.inject:1")
    implementation("io.micronaut:micronaut-validation")
    /*implementation("io.micronaut.groovy:micronaut-runtime-groovy") {
        exclude group: 'org.codehaus.groovy', module: 'groovy-bom'
    }*/
    implementation("com.dropbox.core:dropbox-core-sdk:${dropboxVersion}")
    implementation("io.micronaut:micronaut-http-client")
    runtimeOnly("ch.qos.logback:logback-classic")

    implementation("org.codehaus.groovy:groovy-all:${groovyVersion}")
    implementation("org.codehaus.groovy:groovy-yaml:${groovyVersion}")
    implementation("org.codehaus.groovy:groovy-dateutil:${groovyVersion}")


    // Thymeleaf support
    implementation("io.micronaut.views:micronaut-views-thymeleaf")
}


application {
    mainClass.set("io.github.joxebus.groovywebconsole.Application")
}

java {
    sourceCompatibility = JavaVersion.toVersion("1.8")
    targetCompatibility = JavaVersion.toVersion("1.8")
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}

// heroku configuration
task stage {
    dependsOn 'build', 'clean'
}

build.mustRunAfter clean

gradle.taskGraph.whenReady {
    taskGraph ->
        if (taskGraph.hasTask(stage)) {
            test.enabled = false
        }
}


